import win.ui;
/*DSG{{*/
var winform = win.form(text="休息一下";left=0;top=0;right=600;bottom=600;bgcolor=8421376;border="none";exmode="toolwindow";mode="popup")
winform.add(
date={cls="static";text="11月11日 星期八";left=100;top=160;right=500;bottom=208;align="center";aw=1;color=15793151;font=LOGFONT(h=-27;name='微软雅黑');transparent=1;z=4};
mainBtn={cls="static";text='\u23F3 稍后提醒 (ctrl + x)';left=100;top=540;right=500;bottom=570;align="center";aw=1;color=15793151;font=LOGFONT(h=-20;name='微软雅黑');notify=1;transparent=1;z=1};
progress={cls="plus";left=200;top=360;right=400;bottom=372;aw=1;border={color=-1040;radius=10;width=2};forecolor=15793151;z=2};
static2={cls="static";text="10s";left=200;top=368;right=400;bottom=392;align="center";aw=1;color=15793151;font=LOGFONT(h=-18;name='微软雅黑');transparent=1;z=6};
static4={cls="static";text="活动身体，看向远方……";left=100;top=264;right=500;bottom=328;align="center";aw=1;color=15793151;font=LOGFONT(h=-37;name='微软雅黑');transparent=1;z=5};
time={cls="static";text="11:11";left=200;top=72;right=400;bottom=152;align="center";aw=1;color=15793151;font=LOGFONT(h=-70;name='微软雅黑');ownerDraw=1;transparent=1;z=3}
)
/*}}*/

import thread.event;
import thread.table;
var timeEvent = thread.event("time_s GUID:3CDA0FC7-A96D-4850-9A28-DA1DD4464B44")
var shareT = thread.table("shareT");
var remainNum=0;
win.setTopmost(winform.hwnd);
winform.progress.setProgressRange(0,1000);

// 屏蔽 alt + F4；设置 ctrl + x 关闭
import win.ui.accelerator;
var accelerator = win.ui.accelerator({
    { 
        alt = true; vkey = 0x73/*_VK_F4*/; 
    }; 
    { 
        ctrl = true; vkey = 'X'#; oncommand = function(id,event){ timeEvent.set(); }
    };  
},winform );



winform.mainBtn.oncommand = function(id,event){
timeEvent.set();
}

winform.mainBtn.wndproc = function(hwnd,message,wParam,lParam){
    import win.cur;
	if(message=0x200/*_WM_MOUSEMOVE*/){
		win.cur.load(32649/*_IDC_HAND*/)
		win.cur.setCur()
	}	
}

var IntervalID=winform.setInterval( 
	-1,function(){
		import time;
		

			winform.time.text=tostring(time(,"%H:%M:%S"));
			winform.date.text=tostring(time(,"%Y-%m-%d, %A"));
			
			//var remainNum= shareT.showTime-(time().getMilliTime()-inTime)
			
			winform.progress.stepProgress(-math.round(1000/(shareT.showTime/1000+1)));

			
			winform.static2.text=math.round(remainNum)+"s";
			remainNum-=1;

	} 
)
winform.wndproc = function(hwnd,message,wParam,lParam){ 
	if(message=0x18/*_WM_SHOWWINDOW*/&&wParam=true){
		//在当前显示器全屏置顶
		import sys.monitor;
		win.setRect(winform.hwnd,sys.monitor.getInfoFromWindow( winform.hwnd).rcMonitor);
		win.setForeground(winform.hwnd)
		win.setTopmost(winform.hwnd);
		
		winform.progress.stepProgress(1000);
		remainNum=shareT.showTime/1000;
		winform.changeInterval(IntervalID,1000);
		winform.mainBtn.show(true)
		if(shareT.hasLate&&shareT.lateNum<2){
			winform.mainBtn.text="⏳ 稍后提醒 (ctrl + x)"
		}elseif(shareT.hasPass){
			winform.mainBtn.text="⏳ 下次提醒 (ctrl + x)"
		}else {
			winform.mainBtn.show(false)
		}
		return true; 
		
	}elseif(message=0x18/*_WM_SHOWWINDOW*/&&wParam=false){
		winform.changeInterval(IntervalID,-1);
	}
	
}


win.loopMessage();
return winform;