import win.ui;
import fsys.dlg;
import string.erb;
import string.utils;
import bootstrap.javabuilder;
import bootstrap.webappbuilder;
import fsys.config;
/*DSG{{*/
mainForm = win.form(text="Nutz项目脚手架创建工具";right=655;bottom=407;border="dialog frame";max=false)
mainForm.add(
DataSourceStatic={cls="static";text="DataSource";left=288;top=168;right=356;bottom=194;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=13};
createButton={cls="button";text="生成";left=48;top=336;right=177;bottom=363;font=LOGFONT(name='微软雅黑');tabstop=1;z=25};
dataSource={cls="combobox";left=360;top=168;right=529;bottom=194;edge=1;font=LOGFONT(name='微软雅黑');items={"HikariCP";"Druid";"C3P0";"Tomcat JDBC Pool";"Apache DBCP"};mode="dropdownlist";tabstop=1;z=14};
databaseAdapter={cls="combobox";left=120;top=168;right=280;bottom=194;edge=1;font=LOGFONT(name='微软雅黑');items={"mysql";"postgresql";"oracle";"h2"};mode="dropdownlist";tabstop=1;z=12};
databaseAdapterStatic={cls="static";text="数据库类型";left=40;top=168;right=108;bottom=194;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=11};
databaseHost={cls="edit";left=360;top=200;right=480;bottom=226;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=18};
databaseHostStatic={cls="static";text="数据库地址";left=288;top=200;right=356;bottom=226;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=17};
databaseName={cls="edit";left=120;top=200;right=280;bottom=226;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=16};
databaseNameStatic={cls="static";text="数据库名";left=40;top=200;right=108;bottom=226;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=15};
databasePassword={cls="edit";left=288;top=240;right=354;bottom=266;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=24};
databasePasswordStatic={cls="static";text="数据库密码";left=208;top=240;right=276;bottom=266;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=23};
databasePort={cls="edit";text="3306";left=552;top=200;right=608;bottom=226;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');num=1;tabstop=1;z=20};
databasePortStatic={cls="static";text="数据库端口";left=480;top=200;right=548;bottom=226;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=19};
databaseUsername={cls="edit";left=120;top=240;right=200;bottom=266;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=22};
databaseUsernameStatic={cls="static";text="数据库用户名";left=40;top=240;right=112;bottom=266;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=21};
groupbox1={cls="groupbox";text="项目设置";left=32;top=24;right=624;bottom=128;edge=1;font=LOGFONT(name='微软雅黑');z=1};
groupbox2={cls="groupbox";text="数据库设置";left=32;top=144;right=624;bottom=280;edge=1;font=LOGFONT(name='微软雅黑');z=10};
hasJUnit={cls="checkbox";text="是否使用JUnit";left=504;top=48;right=608;bottom=64;font=LOGFONT(name='微软雅黑');z=6};
packageName={cls="edit";left=336;top=48;right=497;bottom=74;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=5};
packageNameLabel={cls="static";text="项目包名";left=272;top=48;right=333;bottom=76;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=4};
projectName={cls="edit";left=96;top=48;right=257;bottom=74;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=3};
projectNameLabel={cls="static";text="项目名";left=40;top=48;right=101;bottom=76;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=2};
projectPath={cls="edit";left=96;top=88;right=381;bottom=114;autovscroll=false;edge=1;font=LOGFONT(name='微软雅黑');tabstop=1;z=8};
projectPathButton={cls="button";text="选择..";left=400;top=88;right=494;bottom=114;font=LOGFONT(name='微软雅黑');tabstop=1;z=9};
projectPathStatic={cls="static";text="项目地址";left=32;top=88;right=100;bottom=114;align="center";center=1;font=LOGFONT(name='微软雅黑');transparent=1;z=7}
)
/*}}*/

mainForm.projectPath.oncommand = function(id,event){
    //mainForm.msgbox( mainForm.projectPath.text );
}

mainForm.projectPathButton.oncommand = function(id,event){
    mainForm.projectPath.text = fsys.dlg.opendir();
}

var databaseConfig = fsys.config("/res/config/database");

mainForm.databaseAdapter.oncommand = function(id,event){
    if (event == 0x1/*_CBN_SELCHANGE*/) {
        mainForm.databasePort.text = databaseConfig[mainForm.databaseAdapter.selText].port
        if("h2" == mainForm.databaseAdapter.selText){
            mainForm.databaseName.disabled = true;
            mainForm.databaseHost.disabled = true;
            mainForm.databasePort.disabled = true;
            mainForm.databaseUsername.disabled = true;
            mainForm.databasePassword.disabled = true;

            mainForm.databaseName.text = "";
            mainForm.databaseHost.text = "";
            mainForm.databasePort.text = "";
            mainForm.databaseUsername.text = "sa";
            mainForm.databasePassword.text = "";
        }
        else {
            mainForm.databaseUsername.text = "";

            mainForm.databaseName.disabled = false;
            mainForm.databaseHost.disabled = false;
            mainForm.databasePort.disabled = false;
            mainForm.databaseUsername.disabled = false;
            mainForm.databasePassword.disabled = false;
        }
/**
        if (mainForm.databaseAdapter.selIndex == 1) {
            mainForm.databasePort.text = "";
        }
        else {
            // mainForm.databasePort.text = _defaultDatabaseInfo[mainForm.databaseAdapter.selText][port]
/*
            if ("mysql" == mainForm.databaseAdapter.selText) {
                mainForm.databasePort.text = "3306";
            }
            if ("postgresql" == mainForm.databaseAdapter.selText) {
                mainForm.databasePort.text = "5432";
            }
*/
            mainForm.databasePort.text = databaseConfig[mainForm.databaseAdapter.selText].port
        }
**/
    }
}

var dataSourceConfig = fsys.config("/res/config/dataSource")

/**
mainForm.dataSource.oncommand = function(id,event){
    if (event == 0x1/*_CBN_SELCHANGE*/) {
        mainForm.msgbox(mainForm.dataSource.selText);
        mainForm.msgbox(mainForm.hasJUnit.checked == true);
    }
}
**/

mainForm.createButton.oncommand = function(id,event){
    if (mainForm.projectName.text == "") {
        mainForm.msgbox("请输入项目名");
        return;
    }
    if (mainForm.packageName.text == "") {
        mainForm.msgbox("请输入项目包名");
        return;
    }
    if (mainForm.projectPath.text == "") {
        mainForm.msgbox("请输入项目地址");
        return;
    }
/*
    if (mainForm.databaseAdapter.selIndex == 1) {
        mainForm.msgbox("请选择数据库类型");
        return;
    }
*/

    var projectPath = fsys.path.append(mainForm.projectPath.text, mainForm.projectName.text)
    if (!io.exist(projectPath)) {
        fsys.createDir(projectPath);
    }
    else {
        if (mainForm.msgboxTest("指定的项目地址已存在，是否删除已有内容？", "是否删除")) {
            fsys.delete(projectPath);
            fsys.createDir(projectPath);
        }
        else {
            return;
        }
    }

    var junit = mainForm.hasJUnit.checked

    var projectName = string.utils.javaStyle(mainForm.projectName.text);

    // 得到各个路径
    var packagePath = string.join(string.split(mainForm.packageName.text, '.'), '/');
    var srcPath = fsys.path.append(projectPath, 'src/main/java', packagePath, string.lower(projectName));
    var resourcesPath = fsys.path.append(projectPath, 'src/main/resources');
    var testPath = fsys.path.append(projectPath, 'src/test/java', packagePath, string.lower(projectName));

    // 生成必备的路径
    fsys.createDir(srcPath);
    fsys.createDir(resourcesPath);
    if (junit) {
        fsys.createDir(testPath);
    }

    fsys.createDir(resourcesPath + "/chain");
    fsys.createDir(resourcesPath + "/custom");
    fsys.createDir(resourcesPath + "/ioc");

    // gitignore
    ..fsys.copy("/res/templates/.gitignore", projectPath + "/.gitignore");

    // pom.xml
    var pomTemplate = string.erb(string.load("/res/templates/pom.xml.erb"))
    var pomOption = {group_id = mainForm.packageName.text; project_name = mainForm.projectName.text}
    pomOption["database_dependency"] = databaseConfig[mainForm.databaseAdapter.selText].dependency
    pomOption["datasource_dependency"] = dataSourceConfig[mainForm.dataSource.selText].dependency
    pomOption["junit"] = ""
    if (junit) {
        pomOption["junit"]="<dependency>
          <groupId>junit</groupId>
          <artifactId>junit</artifactId>
          <version>4.12</version>
          <scope>test</scope>
        </dependency>"
    }
    string.save(projectPath + "/pom.xml", pomTemplate.format(pomOption))

    // web.properties
    var webTemplate = string.erb(string.load("/res/templates/src/main/resources/web.properties.erb"));
    var webOption = {absolute_root_path = projectPath + "/src/main/webapp"};
    string.save(resourcesPath + "/web.properties", webTemplate.format(webOption))

    // log4j.properties
    var log4jTemplate = string.erb(string.load("/res/templates/src/main/resources/log4j.properties.erb"));
    var log4jOption = {java_style_project_name = string.upper(projectName)};
    string.save(resourcesPath + "/log4j.properties", log4jTemplate.format(log4jOption))

    // chain
    string.save(resourcesPath + "/chain/default.js", string.load("/res/templates/src/main/resources/chain/default.js"))

    // db.properties
    var dbTemplate = string.erb(string.load("/res/templates/src/main/resources/custom/db." + mainForm.databaseAdapter.selText + ".properties.erb"));
    var dbOption = {}
    // h2
    if("h2" == mainForm.databaseAdapter.selText){
        fsys.createDir(projectPath + "/db");
        dbOption = {
            host = projectPath;
            database = string.lower(projectName)
        }
    } else {
        dbOption = {
            host = mainForm.databaseHost.text;
            port = mainForm.databasePort.text;
            database = mainForm.databaseName.text;
            username = mainForm.databaseUsername.text;
            password = mainForm.databasePassword.text
        }
    }
    string.save(resourcesPath + "/custom/db.properties", dbTemplate.format(dbOption))
    // oracle
/*
    if ("oracle" == mainForm.databaseAdapter.selText) {
        fsys.createDir(projectPath + "/lib");
    }
*/

    // dao.js
    var daoTemplate = string.erb(string.load("/res/templates/src/main/resources/ioc/dao.js.erb"));
    var daoOption = {data_source = dataSourceConfig[mainForm.dataSource.selText].dataSource}
    string.save(resourcesPath + "/ioc/dao.js", daoTemplate.format(daoOption))

    // conf.js
    var confTemplate = string.erb(string.load("/res/templates/src/main/resources/ioc/conf.js.erb"));
    var confOption = {class_package = string.lower(mainForm.packageName.text) + "." + string.lower(projectName)}
    string.save(resourcesPath + "/ioc/conf.js", confTemplate.format(confOption))


    bootstrap.javabuilder(srcPath, mainForm.packageName.text, projectName).build();
    bootstrap.webappbuilder(fsys.path.append(projectPath, 'src/main'), mainForm.packageName.text, projectName).build();

    mainForm.msgbox("项目脚手架生成完成", "")
}

import win.ui.menu;
var menuFile = win.ui.popmenu(mainForm);
menuFile.add(
    "支付宝捐赠",
    function(id){
        var alipayForm = win.form(text="支付宝捐赠";right=349;bottom=249;border="dialog frame";max=false;image=$"/res/imgs/alipay.jpg");
        alipayForm.setPos(null,null,262,284,null,null);
        alipayForm.show();
    }
);

var menu = win.ui.menu(mainForm);
menu.add("捐赠", menuFile)
//menu.redraw();

mainForm.show()
return win.loopMessage();
