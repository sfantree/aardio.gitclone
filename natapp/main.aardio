import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="NATAPP 调用示例";right=543;bottom=462;bgcolor=16777215;border="dialog frame";exmode="none";max=false;mode="popup")
winform.add(
btnStart={cls="plus";text="重启内网穿透";left=375;top=22;right=523;bottom=61;align="left";bgcolor=-5197169;dr=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=20}};iconText='\uF20E';notify=1;textPadding={left=39};z=3};
edit={cls="richedit";left=22;top=77;right=523;bottom=443;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;link=1;multiline=1;vscroll=1;z=1};
editAuthtoken={cls="plus";left=22;top=10;right=360;bottom=39;align="right";border={bottom=1;color=-6908266};dl=1;dr=1;dt=1;editable="edit";font=LOGFONT(h=-13);textPadding={top=6;bottom=2};z=2};
radioLogDebug={cls="radiobutton";text="调试模式";left=175;top=48;right=260;bottom=68;bgcolor=16777215;checked=1;dl=1;dr=1;dt=1;z=4};
radioLogInfo={cls="radiobutton";text="简单模式";left=288;top=48;right=373;bottom=68;bgcolor=16777215;dr=1;dt=1;z=5}
)
/*}}*/

import win.ui.atom;
var atom,hwnd = winform.atom("{D053A1BA-AB22-4757-8B26-964C9054C846}.natapp");
if(!atom){
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}

import win.util.tray;
var tray = win.util.tray(winform) 
tray.tip = "NATAPP:请输入 Authtoken";

winform.btnStart.oncommand = function(id,event){
	if(natapp){
		natapp.ctrlEvent(0);
		natapp.close();
	}
	
	var token = string.trim(winform.editAuthtoken.text)
	if(!#token){
		return winform.editAuthtoken.editBox.showErrorTip("错误","请输入 Authtoken")
	}
	
	winform.edit.text = "";
	winform.btnStart.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text=''}
	
	import process.natapp;	
	process.natapp.logger = winform.edit;
	natapp  = process.natapp(token,winform.edit,winform.radioLogDebug.checked?"DEBUG":"INFO");
	if(natapp.tunnelUrl){
		tray.tip = "当前穿透网址: " + natapp.tunnelUrl;
		winform.edit.print( "当前穿透网址: " + natapp.tunnelUrl );
		if(natapp.tunnelInfo){
			winform.edit.print(natapp.tunnelInfo)
		}
	}
	else {
		tray.tip = "NATAPP:认证失败";
		winform.edit.print( "认证失败" );	
	}
	
	win.delay(500)
	winform.btnStart.disabledText = null;
}

winform.btnStart.skin({
	background={
		default=0x668FB2B0;
		disabled=0xFFCCCCCC;
		hover=0xFF928BB3
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

winform.editAuthtoken.setCueBannerText("请输入 Authtoken")

winform.onMinimize = function(lParam){
	winform.show(false); 
	return true;
}

winform.onClose = function(hwnd,message,wParam,lParam){
	winform.show(false);
	return true;    
}

winform.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		if( lParam = 0x205/*_WM_RBUTTONUP*/ ){ 
			var pt = ::POINT();
			::User32.GetCursorPos(pt);
			
			win.setForeground(winform.hwnd)
			winform.popmenu.popup(pt.x,pt.y,true )
		}	
		elseif( lParam = 0x203/*_WM_LBUTTONDBLCLK*/ ){
			winform.show(true);
		} 
	}
}

winform.popmenu = win.ui.popmenu(winform);
winform.popmenu.add('显示主窗口',function(id){
	winform.show(true);
})
winform.popmenu.add('查看我的隧道',function(id){
	import process;
	process.openUrl("https://natapp.cn/tunnel/lists");
})
winform.popmenu.add('退出',function(id){
	winform.onClose = null; 
	winform.close(); 
})

import fsys.config;
var config = fsys.config(io.appData("aardio/natapp/config"));
winform.bindConfig( config.winform,{ 
	radiobutton = "checked";
	plus ={ 
		["^edit"] = "text";
	};  
} );

if(#winform.editAuthtoken.text){
	winform.btnStart.oncommand()
}

winform.edit.onlink=function(message,href){

	if( message = 0x202/*_WM_LBUTTONUP*/ ) {
		import process;
		process.openUrl(href);
	}
}

winform.show();

win.loopMessage();