//RUNAS//
import fonts.fontAwesome;
import win.ui.menu;
import win.ui;
/*DSG{{*/
var winform = win.form(text="五笔短语词库";right=977;bottom=602;acceptfiles=1;bgcolor=16777215)
winform.add(
btnHelp={cls="plus";text="格式说明";left=512;top=570;right=605;bottom=587;align="left";color=3947580;db=1;dl=1;font=LOGFONT(h=-13);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF059';notify=1;paddingLeft=20;z=5};
btnLoad={cls="plus";text='\uF093  读取系统短语';left=614;top=558;right=769;bottom=594;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=2};
btnSave={cls="plus";text='\uF019   更新系统短语';left=780;top=558;right=936;bottom=594;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=3};
editPhrase={cls="edit";left=18;top=12;right=962;bottom=552;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-16);hscroll=1;multiline=1;vscroll=1;z=1};
lbResult={cls="static";text="可以直接拖拽文本格式短语文件（*.txt）到这里";left=17;top=565;right=478;bottom=594;color=3947580;db=1;dl=1;dr=1;font=LOGFONT(h=-15);transparent=1;z=4}
)
/*}}*/

import wubi.phrase;
import win.versionEx;
winform.btnLoad.oncommand = function(id,event){
	winform.lbResult.text = "";
	
	if( !win.versionEx.isWin10ReleaseLater("1703") ){
		return winform.msgboxErr("仅支持 Win10 1703以上版本的五笔短语词库")
	}
	
	var wubiUdpPath = wubi.phrase.getWubiUdpPrimaryPath()
	
	if(!wubiUdpPath){
		return winform.msgboxErr("系统短语词库为空");
	}	
	
	winform.editPhrase.text="";
	winform.btnLoad.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'} 
	thread.invoke( function(winform,wubiUdpPath){
		import wubi.phrase;
		var data,err = wubi.phrase.load(wubiUdpPath);
		if(!data){
			winform.msgboxErr(err:"未知错误")	
		}
		else {
			winform.editPhrase.text = data 
		} 
		winform.btnLoad.disabledText = null;
	},winform,wubiUdpPath)
}

var savePhrases = function(phraseText,winform){
	import wubi.phrase;
	import tsfUtil;
	import fsys;
	
	tsfUtil.reset(
		function(){ 
			var wubiUdpPath1 = ..io.getSpecial(0x1a /*_CSIDL_APPDATA*/,"\Microsoft\InputMethod\Chs\ChsWubiEUDPv1.lex")
			var wubiUdpPath = ..io.getSpecial(0x1a /*_CSIDL_APPDATA*/,"\Microsoft\InputMethod\Chs\ChsWubiEUDPv2.lex")
	
			for(i=1;10;1){
				process.kill("ctfmon.exe")
				process.kill("ChsIME.exe")
				
				if(wubiUdpPath1)fsys.delete(wubiUdpPath1 )  
				if(wubiUdpPath)fsys.delete(wubiUdpPath )
				if((!io.exist(wubiUdpPath1)) && (!io.exist(wubiUdpPath)) ){ break; }  
				
				sleep(1000)
			}
			
			var ret,err = wubi.phrase.save(phraseText);
			if(!ret){
				return winform.msgboxErr(err:"未知错误")
			} 	
		}
	)
		
	winform.btnSave.disabledText = null;
	winform.lbResult.text = "更新成功,如果不能输入请按ALT+SHIFT切换一次即可"
}

winform.btnSave.oncommand = function(id,event){
	
	if( !win.version.isWin10ReleaseLater("1703") ){
		return winform.msgboxErr("仅支持 Win10 1703以上版本的五笔短语词库")
	}
	
	var str = string.trim(winform.editPhrase.text);
	if(!#str){
		winform.editPhrase.text = wubi.phrase.default;
		winform.msgbox("短语内容不能为空")
		return;
	}
	
	winform.lbResult.text = "";
	winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	winform.editPhrase.setFocus();
	thread.invoke( savePhrases,winform.editPhrase.text,winform)
}

var loadPhraseFromFile = function(winform,path){
	import fsys;
	import wubi.phrase;
	import fsys.codepage;
	
	winform.lbResult.text = "";
	
	if(..fsys.isDir(path)){
		return winform.msgboxErr("请指定文件而不是目录路径")
	}
	
	var file = ..io.open(path,"rb");
	if(!file){
		return winform.msgboxErr("打开文件失败")
	}
		
	if(file.read(8)=="mschxudp"){
		file.close();
		winform.editPhrase.text = wubi.phrase.load(path)
	}	
	
	winform.editPhrase.text = ..fsys.codepage.load(path);
}


import fsys.dlg;
winform.editPhrase.enablePopMenu({
	{ /*---分隔线---*/ };
	{
		"保存短语文件";
		function(id){
			var path = fsys.dlg.save("*.txt|*.txt||","保存短语到文本文件",,winform)
			if(path)string.save(path,winform.editPhrase.text )
		} 
	};
	{
		"加载短语文件";
		function(id){
			var path = fsys.dlg.open("*.txt|*.txt||","自文本文件加载短语",,winform)
			if(path) thread.invoke(loadPhraseFromFile,winform,path)
		} 
	};
	{ /*---分隔线---*/ };
	{
		"恢复到范例";
		function(id){
			winform.editPhrase.text = wubi.phrase.default;
		} 
	};
})

import fsys;
import process.admin;
process.admin.enableDropMsg(winform.hwnd) 
winform.onDropFiles = function(files){
	thread.invoke(loadPhraseFromFile,winform,files[1])
}

import style;
winform.btnLoad.skin(style.primaryButton)
winform.btnSave.skin(style.primaryButton)

winform.editPhrase.limit = 0;
winform.editPhrase.text = wubi.phrase.default;

winform.editPhrase.translateAccelerator = function(msg){ 
    var vk = msg.wParam;
 	if( vk == 0x9/*_VK_TAB*/ ){
		if( msg.message == 0x100/*_WM_KEYDOWN*/) {
			winform.editPhrase.selText = '\t'; 
			return true;
		}
	}	
}

winform.btnHelp.oncommand = function(id,event){
	var phraseHelp = winform.loadForm("\dlg\dict\phraseHelp.aardio");
	phraseHelp.doModal()
}

winform.btnHelp.skin(style.plainButton)

winform.show();
win.loopMessage();
return winform;