import win.ui;
/*DSG{{*/
mainForm = win.form(text="aardio form";right=584;bottom=421)
mainForm.add(
button={cls="button";text="Explorer";left=504;top=21;right=566;bottom=43;ah=1;aw=1;z=3};
button2={cls="button";text="Update";left=504;top=389;right=566;bottom=411;ah=1;aw=1;z=17};
button3={cls="button";text="Check";left=416;top=389;right=478;bottom=411;ah=1;aw=1;z=18};
button4={cls="button";text="Explorer";left=504;top=66;right=566;bottom=88;ah=1;aw=1;z=21};
edit={cls="edit";left=108;top=21;right=497;bottom=43;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=2};
edit2={cls="edit";left=139;top=106;right=528;bottom=128;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=6};
edit3={cls="edit";left=139;top=128;right=528;bottom=150;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=8};
edit4={cls="edit";left=139;top=151;right=528;bottom=173;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=10};
edit5={cls="edit";left=139;top=173;right=528;bottom=195;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=12};
edit6={cls="edit";left=139;top=195;right=528;bottom=217;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=14};
edit7={cls="edit";left=108;top=43;right=497;bottom=65;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=16};
edit8={cls="edit";left=108;top=66;right=497;bottom=88;acceptfiles=1;ah=1;aw=1;edge=1;tabstop=1;z=20};
listview={cls="listview";left=14;top=224;right=566;bottom=377;ah=1;aw=1;edge=1;z=4};
static={cls="static";text="Unpack";left=18;top=21;right=93;bottom=43;ah=1;aw=1;center=1;transparent=1;z=1};
static2={cls="static";text="AccessKey";left=18;top=107;right=134;bottom=129;ah=1;aw=1;center=1;transparent=1;z=5};
static3={cls="static";text="CovertINIFile";left=18;top=129;right=134;bottom=151;ah=1;aw=1;center=1;transparent=1;z=7};
static4={cls="static";text="gb2big5";left=18;top=151;right=134;bottom=173;ah=1;aw=1;center=1;transparent=1;z=9};
static5={cls="static";text="PSLHardCodedString";left=18;top=173;right=134;bottom=195;ah=1;aw=1;center=1;transparent=1;z=11};
static6={cls="static";text="WebTranslate";left=18;top=195;right=134;bottom=217;ah=1;aw=1;center=1;transparent=1;z=13};
static7={cls="static";text="Unpack par";left=18;top=43;right=93;bottom=65;ah=1;aw=1;center=1;transparent=1;z=15};
static8={cls="static";text="Unpack dir";left=18;top=66;right=93;bottom=88;ah=1;aw=1;center=1;transparent=1;z=19}
)
/*}}*/

import fsys;
import fsys.dlg;
import win.reg;
import io;

io.open();
//初始化7zip执行文件
var sevenzip_table = {
					root = "HKEY_CURRENT_USER";
					subkey = "SOFTWARE\7-Zip";
					reg_str = "Path";
					unpack_par = //-- x -y -r "%s" -o"%s"
					};
var sevenzip_reg = win.reg(sevenzip_table.root ++ "\" ++ sevenzip_table.subkey, true);
var sevenzip_dir = sevenzip_reg.queryValue(sevenzip_table.reg_str);
sevenzip_reg.close();

if(io.exist(fsys.joinpath(sevenzip_dir, "7z.exe"))){
	mainForm.edit.text = fsys.joinpath(sevenzip_dir, "7z.exe");
}

mainForm.edit7.text = sevenzip_table.unpack_par;

//初始化edit下载地址
var psl_url = {
				AccessKey = "http://jp.wanfutrade.com/download/PslAccessKey_Modified_wanfu.rar";
				CovertINIFile = "http://jp.wanfutrade.com/download/CovertINIFile.rar";
				gb2big5 = "http://jp.wanfutrade.com/download/PSLGbk2Big5_Modified_wanfu.rar";
				PSLHardCodedString = "http://jp.wanfutrade.com/download/PSLHardCodedString.rar";
				WebTranslate = "http://jp.wanfutrade.com/download/PSLWebTrans.rar";
				}; 
mainForm.edit2.text = psl_url.AccessKey;
mainForm.edit3.text = psl_url.CovertINIFile;
mainForm.edit4.text = psl_url.gb2big5;
mainForm.edit5.text = psl_url.PSLHardCodedString;
mainForm.edit6.text = psl_url.WebTranslate;
mainForm.edit8.text = io.fullpath("/download/Macros");

//初始化listview控件
mainForm.listview.checkbox = true;
mainForm.listview.insertColumn("url",150)
mainForm.listview.insertColumn("filename",150)
mainForm.listview.insertColumn("status",70)
mainForm.listview.insertColumn("size",50)
mainForm.listview.insertColumn("speed",50)
mainForm.listview.insertColumn("download",90)
mainForm.listview.adjust = function(cx,cy){
    mainForm.listview.fillParent(1)
};

//创建下载线程管理器
import thread.dlManager;
var dlmgr = thread.dlManager(5, 1000/*最多允许五个线程同时下载*/);

//响应下载事件
dlmgr.onReceiveBegin = function(
    id,url,filename,statusText,httpStatusCode,totalSize,downSize){
    mainForm.listview.setItemText(
    {url;filename;statusText;fsys.formatSize(totalSize);fsys.formatSize(downSize) },id )
}
dlmgr.onReceive = function(id,sizePs,downSize){
    mainForm.listview.setItemText( fsys.formatSize(downSize),id,6)
    mainForm.listview.setItemText( fsys.formatSize(sizePs) ++ "/s" ,id,5)
}
dlmgr.onEnd = function(id,savepath,resumePath,contentLength){ 
	if( savepath ){ 
        mainForm.listview.setItemText(  "已完成"  ,id,3)
        mainForm.listview.setItemText( fsys.formatSize(contentLength),id,4)
        mainForm.listview.setChecked(id, false)
        thread.create(
        	function( ... ){
        		import win;
        		import process;
        		import fsys;
        		import fsys.path;
        		var unpack_par = { ... };
       			var prcs, prcserr, prcscode = process(unpack_par[1],
       					string.format(unpack_par[3], unpack_par[2],
       					unpack_par[4]))
       			prcs.waitOne()
       			if(fsys.path.isDir(fsys.joinpath(unpack_par[4], "Doc"))){
       				fsys.move(fsys.joinpath(unpack_par[4], "*.txt"),
       					fsys.joinpath(unpack_par[4], "Doc"))
       			} else {
       				fsys.createDir(fsys.joinpath(unpack_par[4], "Doc"))
       				fsys.move(fsys.joinpath(unpack_par[4], "*.txt"),
       					fsys.joinpath(unpack_par[4], "Doc"))
       			}
       			if(fsys.path.isDir(fsys.joinpath(unpack_par[4], "Data"))){
       				fsys.move(fsys.joinpath(unpack_par[4], "*.dat"),
       					fsys.joinpath(unpack_par[4], "Data"))
       			} else {
       				fsys.createDir(fsys.joinpath(unpack_par[4], "Data"))
       				fsys.move(fsys.joinpath(unpack_par[4], "*.dat"),
       					fsys.joinpath(unpack_par[4], "Data"))
       			}       			
        	},
        	mainForm.edit.text, savepath,
        	mainForm.edit7.text, mainForm.edit8.text
        )
    }
    else {
        mainForm.listview.setItemText(  "已停止"  ,id,3)
    }
   	//mainForm.button3.disabled = false;
    mainForm.listview.setItemText(  "0KB/s"  ,id,5)
    //fsys.delete(resumePath)
}
dlmgr.onError = function(id,err){
    mainForm.listview.setItemText( err,id,3);
  	//mainForm.button3.disabled = false;

}

mainForm.button2.oncommand = function(id,event){

	if(mainForm.listview.count){
		for(k,v in mainForm.listview.checked){
			dlmgr.push(
			id =  v;
			url = mainForm.listview.getItemText(v, 1);
			filename=""; //文件名可以省略
 	        savedir = "/download/";
 	        ) 
		}
	} else {
	
	//添加下载任务非常简单,push下载参数就可以了
   	//注意这里为了简化示例,任务id使用了listview控件的行号,如果需要删除行则需要使用一个表保持id与行号的映射关系
 	  for(name,ctrl in mainForm.eachControl("edit") ){
 	      if(string.match(name, "edit[2-6]")){
 	          dlmgr.push(
 	          id =  mainForm.listview.addItem( win.getText(ctrl.hwnd) );
 	          url = win.getText(ctrl.hwnd);
 	          filename=""; //文件名可以省略
 	          savedir = "/download/"; 
 	          )
 	     }
 	  }
   }
    mainForm.button2.disabled = true;
    mainForm.button3.disabled = true;
    dlmgr.works.wait()
	if(dlmgr.works.finished()){
		mainForm.button3.disabled = false;
	} else {
		mainForm.button3.disabled = true;
	}

}
mainForm.button3.oncommand = function(id,event){
    function check_mod_edit(edit_mod_text){
        var edit_changed = false; 
    	for hItem in mainForm.listview.each(){
			if(mainForm.listview.getItemText(hItem, 1) == edit_mod_text){
				return false; 
			} else {
				edit_changed = true;
			}
		}
		return edit_changed; 
    }
    
    if(mainForm.edit2.modified){
    	if(check_mod_edit(mainForm.edit2.text)){
    		mainForm.listview.addItem(mainForm.edit2.text)
    	}
    }
    elseif(mainForm.edit3.modified){
    	if(check_mod_edit(mainForm.edit3.text)){
    		mainForm.listview.addItem(mainForm.edit3.text)
    	}    
    }
    elseif(mainForm.edit4.modified){
    	if(check_mod_edit(mainForm.edit4.text)){
    		mainForm.listview.addItem(mainForm.edit4.text)
    	}    
    }
    elseif(mainForm.edit5.modified){
    	if(check_mod_edit(mainForm.edit5.text)){
    		mainForm.listview.addItem(mainForm.edit5.text)
    	}    
    }
    elseif(mainForm.edit6.modified){
    	if(check_mod_edit(mainForm.edit6.text)){
    		mainForm.listview.addItem(mainForm.edit6.text)
    	}    
    }
    
	for hItem in mainForm.listview.each(){
		if(mainForm.listview.getItemText(hItem, 3) != "已完成"){
			mainForm.listview.setChecked(hItem, true)	
		}
	}
	mainForm.button2.disabled = false;
}

//下载任务右键管理菜单
import win.ui.menu;
mainForm.listview.onnotify = function(id,code,ptr){  
      if( code = 0xFFFFFFFB/*_NM_RCLICK*/ ){
      
          var x,y = win.getMessagePos();  
          var nmListView = mainForm.listview.getNotifyMessage(code,ptr);
          
          //创建弹出菜单
        var popmenu = win.ui.popmenu(mainForm); 
        if(nmListView.iItem){
        	popmenu.add('取消',function(id){ 
            dlmgr.cancel( nmListView.iItem )
            if(mainForm.listview.getItemText(nmListView.iItem, 3) == "已完成"){
            	mainForm.listview.delItem(nmListView.iItem)           	
            }
            elseif(mainForm.listview.getChecked(nmListView.iItem)){
            	mainForm.listview.delItem(nmListView.iItem)
            } 
        	} )
        	popmenu.popup(x,y,true);
        	popmenu.close();
        }
        
      }
}

//关闭窗体时停止所有下载
import thread.event;
mainForm.onClose = function(hwnd,message,wParam,lParam){
    mainForm.text = "正在等待关闭"; 
    dlmgr.quit();
}

//浏览7zip执行文件
mainForm.button.oncommand = function(id,event){
	mainForm.edit.text = fsys.dlg.open("执行文件|*.exe|所有文件|*.*");
}

//浏览解压目录
mainForm.button4.oncommand = function(id,event){
	mainForm.edit8.text = fsys.dlg.opendir("/download/");
}

mainForm.show();
return win.loopMessage();