import fonts.fontAwesome;
import win.ui;
import win.dlg.message;
import soImage;
import zbar;
/*DSG{{*/
var winform = win.form(text="解密";right=599;bottom=439;bgcolor=16777215)
winform.add(
btnCipherText={cls="plus";text='\uF084   密文解密';left=89;top=346;right=273;bottom=390;bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=4};
btnPrScrn={cls="plus";text='\uF029   截图解密';left=350;top=346;right=534;bottom=390;bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=2};
edtAccount={cls="edit";left=217;top=217;right=459;bottom=252;edge=1;font=LOGFONT(h=-20);multiline=1;readonly=1;z=3};
edtCipherText={cls="edit";left=217;top=30;right=459;bottom=130;autohscroll=false;edge=1;multiline=1;z=5};
edtPassword={cls="edit";left=217;top=278;right=459;bottom=313;edge=1;font=LOGFONT(h=-20);multiline=1;readonly=1;z=6};
edtTOTP={cls="plus";left=217;top=158;right=459;bottom=193;border={bottom=1;color=-16744448};color=255;font=LOGFONT(h=-20;name='微软雅黑');textPadding={top=6;bottom=1};z=12};
lbTip={cls="static";left=63;top=523;right=695;bottom=572;db=1;dl=1;dr=1;transparent=1;z=1};
plusPwd={cls="plus";left=487;top=288;right=512;bottom=305;background="\res\visible.png";notify=1;z=11};
static={cls="static";text="密文";left=123;top=60;right=191;bottom=88;font=LOGFONT(h=-16);transparent=1;z=7};
static2={cls="static";text="动态码";left=123;top=161;right=191;bottom=189;font=LOGFONT(h=-16);transparent=1;z=8};
static3={cls="static";text="帐户";left=123;top=220;right=191;bottom=248;font=LOGFONT(h=-16);transparent=1;z=9};
static4={cls="static";text="密码";left=123;top=283;right=191;bottom=311;font=LOGFONT(h=-16);transparent=1;z=10}
)
/*}}*/

import style;
winform.btnCipherText.skin(style.button);
winform.btnPrScrn.skin(style.button);
winform.edtTOTP.createEditBox( 
	cls = "edit";
	num = 1;  
	limit = 6;
);

winform.edtPassword.passwordChar = "*";
winform.edtPassword.wndproc = function(hwnd,message,wParam,lParam){
	if(message=7/*_WM_SETFOCUS*/){//获取焦点
        winform.plusPwd.setBackground("\res\invisible.png")
        winform.edtPassword.passwordChar = null;
	}
	if(message=8/*_WM_KILLFOCUS*/){//失去焦点
        winform.plusPwd.setBackground("\res\visible.png")
        winform.edtPassword.passwordChar = "*";
	}
}

winform.plusPwd.wndproc = function(hwnd,message,wParam,lParam){
	if(message=0x200/*_WM_MOUSEMOVE*/){//鼠标进入控件区域
        winform.plusPwd.setBackground("\res\invisible.png")
        winform.edtPassword.passwordChar = null;        
	}
	if(message=0x02A3/*_WM_MOUSELEAVE */){//鼠标离开控件区域
        winform.plusPwd.setBackground("\res\visible.png")
        winform.edtPassword.passwordChar = "*";
	}
}

var passport_decrypt = function(str,key="BOB"){
	var str = mainForm.passport_key(crypt.bin.decodeUrlBase64(str),key);
	var tmp = "";
	for(i=1;#str;1){
		j = string.split(str)[i];
		i++;
		tmp ++= string.pack(string.unpack(string.split(str)[i])^string.unpack(j));
	}
	return tmp;
}

var decrypt = function(){
	var code = string.trim(winform.edtTOTP.text);
	if(not string.find(code ,"^\d{6}$") ) {
    	winform.msgWarn("动态码不规范",500);
    	return ;
    }
    
    var cipherText = string.trim(winform.edtCipherText.text);
 	if(!#cipherText) {
    	winform.msgWarn("密文未填写",500);
    	return ;
    }
    
    var mergeStr,err = mainForm.rpc.authcode(cipherText,"DECODE",code);
    if(err=="otperr"){
    	winform.msgWarn("动态码验证失败",500);
    	return ;     	
    }
    if(!#mergeStr){
    	winform.msgErr("密文验证失败",500);
    	return ;
    }
    
    t = string.split(mergeStr,"|");
    winform.edtAccount.text = passport_decrypt(t[2]);
    winform.edtPassword.text = passport_decrypt( mainForm.rpc.aescode(t[1],"DECODE") );
}


winform.btnCipherText.oncommand = function(id,event){
    var cipherText = string.trim(winform.edtCipherText.text);
    if(!#cipherText){
    	winform.msgWarn("密文为空",500);
    	return ; 
    }
    decrypt();	
}

var dll = raw.loadDll($"\PrScrn.dll");
winform.btnPrScrn.oncommand = function(id,event){
	dll.PrScrn();
	var img = soImage();
	img.fromClipBD();
	img.grayScale();
	
	scanner = zbar.scanner();
	var prscrnTab = {};
	scanner.scan(img.getData(),img.getWidth(),img.getHeight(),function(typeName,data){
    	table.push(prscrnTab,{typeName=typeName;data=data});
	})
	
	if(#prscrnTab){
		winform.edtCipherText.text = string.trim(prscrnTab[1]['data']);
		decrypt();
	} else {
		winform.msgFrown("未检测出有效二维码",300);
	}
}

winform.show();
return winform;